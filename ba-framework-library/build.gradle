apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

boolean isSampleApp = true

if (isSampleApp){
    apply plugin: 'com.android.application'

} else {
    apply plugin: 'com.android.library'
}

Properties localProps = new Properties()
localProps.load(new FileInputStream(file('../local.properties')))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    useLibrary  'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"

        if(isSampleApp)
            applicationId 'com.blackangel.baframework'
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "full"

    productFlavors {
        sampleApp {
            dimension "full"
            applicationIdSuffix ".sample"
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'org.mockito:mockito-core:2.21.0'
    androidTestImplementation 'org.mockito:mockito-android:2.21.0'
    annotationProcessor 'com.android.databinding:compiler:3.0.1'        // databinding 사용시 BR 클래스 심볼 인식등에 문제가 있을때 해결책
    implementation "com.android.support:design:27.1.1"
    implementation "com.android.support:preference-v7:27.1.1"
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.2.3'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    implementation 'com.facebook.android:facebook-android-sdk:4.23.0'
    compileOnly 'org.projectlombok:lombok:1.18.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.0'

}

// bintray 에서 인식하는 그룹명/버전 명시(필수)
group = 'com.blackangel'
version = '1.0.0'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'ba-framework-library'
            description 'This is ba-framework library'
            url 'https://github.com/black2602/ba-framework'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'com.blackangel'
            artifactId 'ba-framework-library'
            version '1.0.0'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/black2602/ba-framework.git'
                url 'https://github.com/black2602/ba-framework'

            }
            developers {
                developer {
                    name 'black2602'
                }
            }
        }
    }
}

bintray {
    user = localProps['BINTRAY.USER']
    key = localProps['BINTRAY.KEY']
    configurations = ['archives']

    publish = true        // aar 파일을 bintray에서 자동으로 배포하게 하려면 publish를 true로 설정해야 합니다. (기본값은 false)
    override = true         // 이미 배포된 버전에서 버전명은 그대로 두고, 소스 수정하여 다시 배포할 때 필요

    pkg {
        repo = 'maven'
        name = 'ba-framework-library'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/black2602/ba-framework'
        version {
            name = '1.0.0'
        }
    }
}